global function Init_ChatCommands
global function AddChatcommand

struct
{
    table< string, array< bool functionref( entity, array<string> ) > > callbacks
} file

void function Init_ChatCommands()
{
    AddCallback_OnReceivedSayTextMessage( MessageReceived )
}

ClServer_MessageStruct function MessageReceived( ClServer_MessageStruct message )
{
    if ( message.message.len() == 0 )
        return message

    foreach( string command, array< bool functionref( entity, array<string> ) > callbacks in file.callbacks )
    {
        if ( split( message.message, " " )[0].tolower() == command.tolower() )
        {
            array<string> args = split( message.message, " " )
            args.remove(0)

            foreach( bool functionref( entity, array<string> ) callback in callbacks )
            {
                callback( message.player, args )
            }

            message.shouldBlock = true
        }
    }

    return message 
}

void function AddChatcommand( string command, bool functionref( entity, array<string> ) callback )
{
    if ( command in file.callbacks )
        file.callbacks[command].append( callback )
    else
        file.callbacks[command] <- [ callback ]
}